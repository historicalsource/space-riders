	.TITLE	PIPE1 - *PIPELINE*
	.SBTTL	*************************
	.SBTTL	*
	.SBTTL	* PROGRAMMER:  DR. J. 
	.SBTTL	*
	.SBTTL	* CHECKER:
	.SBTTL	*
	.SBTTL	*************************
;
;DATE INITATED:  5/18/78 
;PROJECT CHARGE #:  6B945 
;DISK #:  2 YELLOW
;HARDWARE REQUIREMENTS:
;	PINBALL OLD ELECTRONICS UNIVERSAL BOARD
;
;
;
;
;MEMORY REQUIREMENTS:
;	RAM:	256X8 ZERO PAGE
;	ROM:	4K 7000-7FFF
;
;
;
;INTERRUPT REQUIREMENTS:
;	NMI 4 MSEC
;
;
;
;
;ASSEMBLY COMMAND STRING:
;
;
;
;
;LINK COMMAND STRING:	 
;
;
;
;
;
;PROGRAM DESCRIPTION:
;	PIPELINE PINBALL MACHINE
;
;
;
;
;
;
;
;
;
;
;*****DATA STRUCTURES*****
;*
;* PIPELINE FLAG
;* PIPFLG	76543210	1=ACHIEVED
;		PIPELINE	0=NOT ACHIEVED
;* DROP TARGET FLAG
;* DRPFLG	76543210	1=DOWN
;		     RCL	0=UP
;
;
;
;* CAPTIVE TARGET FLAG
;* TRGFLG	76543210	1=ACHIEVED
;		     RCL	0=NOT ACHIEVED
;* ALTERNATE SPECIAL,XBALL,THUMPERS FLAG
;* ALTFLG	76543210	1=ALTERNATION IN PROGRESS
;		     SXT	0=NO ALTERNATION
	.ASECT
	.RADIX	16
	.SBTTL	RAM ALLOCATION
;*DISPLAY
DISP:	.BLKB	10
;*TEMPS
TEMP1:	.BLKB	2
TEMP2:	.BLKB	2
TEMP3:	.BLKB	2
MCHTMP: .BLKB	1		;MATCH TEMP
P14TMP: .BLKB	1		;P1-4 TIMER
TEMP5:	.BLKB	2
TEMPI:	.BLKB	2

	.=1C
;*DISPLAY CONTINUED
BALL:	.BLKB	1		;BALL DISPLAY
CREDIT:	.BLKB	1		;CREDIT DISPLAY

	.=20
;*SCORE STORE AREA
DISP1:	.BLKB	10

	.=30
;*LAMP AREA
LAMPS:	.BLKB	10
SWRAM:				;OP SYS SWIRM
TESTSW:	.BLKB	1		;TEST
LCNSW:	.BLKB	1		;LEFT COIN
RCNSW:	.BLKB	1		;RT COIN
STRTSW:	.BLKB	1		;START
SLMSW:	.BLKB	1		;SLAM
TLTCSW:	.BLKB	1		;TILT CAB
TLTPSW:	.BLKB	1		;TILT PEND
PSWRAM:				;PLAYFIELD
PSDEB:	.BLKB	6		;DEBOUNCE
PSTAT:	.BLKB	6		;STATUS
PSLACH:	.BLKB	6		;LATCH
;*SOLENOID COUNTERS
SOLRAM:
LTHUMP:	.BLKB	1		;LEFT THUMPER
RTHUMP:	.BLKB	1		;RT THUMPER
LSLING:	.BLKB	1		;LFT SLINGSHOT
RSLING:	.BLKB	1		;RT SLINGSHOT
OUTHOL:	.BLKB	1		;OUTHOLE KICKER
LDROP:	.BLKB	1		;LEFT DROP TARGET
CDROP:	.BLKB	1		;CENT DROP TARGET
RDROP:	.BLKB	1		;RT DROP TARGET
LHKICK:	.BLKB	1		;LFT HOLE KICKER
RHKICK:	.BLKB	1		;RT HOLE KICKER
LKCOIL: .BLKB	1		;LOCKOUT COIL
FLIPEN:	.BLKB	1		;FLIPPER CONTROL
SOLMTR:
XBMTR:	.BLKB	1		;XTRA BALL METER
REPMTR:	.BLKB	1		;REPLAY METER
PLAMTR:	.BLKB	1		;PLAYS METER
SPMTR:	.BLKB	1		;SPECIAL METER
TIMTR:	.BLKB	1		;TIME METER
CNMTR:	.BLKB	1		;COIN METER
;*
;*ATTRACT MODE PARAMETERS
;*
ATBLK:  .BLKB 2	;DISP. BLANK TIMER
ATNDEX:	.BLKB 2	;ATTRACT TABLE INDEX
LPCNT:	.BLKB 1	;LOOP INDEX
AFRAME:	.BLKB 1	;FRAME TIME
ATIMER:	.BLKB 1	;ATTRACT TIMER
BNDEX:	.BLKB 2	;BACKGROUND INDEX
BFRAME:	.BLKB 1	;BACKGROUND FRAME
BTIMER:	.BLKB 1	;BACKGROUND TIMER
;*
;*MISC. TEMPS
;*
NMITMP:	.BLKB 2	;INT. TEMP
MSTMP:	.BLKB	2		;MATCH TEMP
TSTTMP:	.BLKB	1		;TEST TEMP
;*
;*SOLENOID AREA
;*

	.=80
SOLB1:	.BLKB	1		;SOLENOIDS
MREP:	.BLKB	1		;MED REPLAY
LOREP:	.BLKB	1		;LO REPLAY
	.BLKB 	1
SOLB2:	.BLKB	1		;SOLENOIDS
LCMODE:	.BLKB	2		;LEFT COIN MODE
	.BLKB	1		;SPARE
SOLB3:	.BLKB	1		;SOLENOIDS
RCMODE:	.BLKB	2		;RT COIN MODE
	.BLKB	1		;SPARE
SOLB4:	.BLKB	1		;SOLENOIDS

	.=8D			;AVOID SOLENOID AREA
;*
;*
;*SOUND AREA
TONST:
TLTCNT:	.BLKB	1		;TILT SOUND
TLTSND:	.BLKB	1
CRDCNT:	.BLKB	1		;CREDIT
CRDSND:	.BLKB	1
STCNT:  .BLKB 	1		;START
STSND:	.BLKB 	1
SPCNT:	.BLKB	1		;SPECIAL
SPSND:	.BLKB	1
SP5CNT:	.BLKB	1		;5K SPECIAL
SP5SND:	.BLKB	1
XBCNT:	.BLKB	1		;XTRA BALL
XBSND:	.BLKB	1
X25CNT: .BLKB	1		;25K XBALL
X25SND:	.BLKB	1
DTBCNT:	.BLKB	1		;DOUBLE/TRIPLE BONUS
DTBSND:	.BLKB	1
BONCNT:	.BLKB	1		;BONUS ADV.
BONSND:	.BLKB	1
K1CNT:	.BLKB	1		;1K PTS
K1SND:	.BLKB	1
HUNCNT:	.BLKB	1		;100 PTS
HUNSND:	.BLKB	1
TENCNT:	.BLKB	1		;10 PTS
TENSND:	.BLKB	1
MCHCNT: .BLKB	1		;MATCH
MCHSND:	.BLKB	1
TONEND:
;*
;*FLAGS + VARIABLES
;*
MAXB:	.BLKB	1		;BALLS/GAME
MAXCRD:	.BLKB	1		;MAX CREDIT
MAXP:	.BLKB	1		;# PLAYERS
PLAYN:	.BLKB	1		;CURRENT PLAYER #
BALLN:	.BLKB	1		;CURRENT BALL #
PIPFLG:	.BLKB	1		;PIPELINE
PMEM:	.BLKB	4		;MEMORY AREA
GMFLG:	.BLKB	1		;GAME FLAG 0=ATT, 1=GAME, FF=SCORED
SCRFLG:	.BLKB	1		;0=NO SCORE, 1=SCORED
TSTFLG: .BLKB	1		;0=NO TEST,1,2,3=TEST
DTBFLG:	.BLKB	1		;0=1X, 1=2X, 2=3X BONUS
BCNT:	.BLKB	1		;BONUS ADVANCE STATE
LCKFLG:	.BLKB	1		;LOCK-OUT COIL FLAG
CRED:	.BLKB	1		;CREDIT
SEED:	.BLKB	1		;MATCH SEED
FLCNT:	.BLKB	1		;PLAYER-UP FLASH
XFCNT:	.BLKB	1		;XBALL FLASH COUNT
LAMPFL:	.BLKB	1		;LAMP TO FLASH
LFCNT:	.BLKB	1		;LMP FLASH LT.
FRTMP:	.BLKB	1		;PHASE COUNTER
DRPCNT:	.BLKB	1		;DROP RESET COUNT
ALTCNT:	.BLKB	1		;SPEC./XBALL ALT
ALTFLG:	.BLKB	1		;ALTERNATION FLAG
XFLG:	.BLKB	1		;XBALL FLAG
LDPFLG:	.BLKB	1		;LEFT DROP TARGET STATE 0-1-2
CDPFLG:	.BLKB	1		;CENT DROP _ARGET STATE 0-1-2-3
RDPFLG:	.BLKB	1		;RIGHT DROP TARGET STATE 0-1-2
RSLDCT:	.BLKB	1		;RT SLINGSHOT DELAY COUNT
LSLDCT:	.BLKB	1		;LFT SLINGSHOT DELAY COUNT
TRGFLG:	.BLKB	1		;CAPTIVE TARGET FLAG
DRPFLG:	.BLKB	1		;DROP TARGET STATUS
LCFLG:	.BLKB	1		;COIN FLAGS
RCFLG:	.BLKB	1		;RT. CHUTE
CDELAY:	.BLKB	1		;COIN MTR DELAY
CNCNT:	.BLKB	1		;COIN MTR COUNT
HKCNT:	.BLKB	1		;HOLE KICKER DEBOUNCE COUNT
HZ:	.BLKB	2		;MIN TIMER
RTEST:	.BLKB	2		;RAM TEST LOCATION
;*
;*CONSTANTS
;*
WRES	=4000			;WAKEUP RESET
AUDON	=3000			;AUDIO ENABLE
AUDOFF	=6000			;AUDIO DISABLE
WAVE	=80			;WAVEFORM
FREQ	=88			;FREQUENCY
AMP	=84			;AMPLITUDE
MAXBON	=30.			;MAXIMUM BONUS
LHOLE	=203C			;HOLE KICKER SW. ADDRS
RHOLE	=203B
OHOLE	=2038
MINTIM	=7500.			;MINUTE TIMER
SEC30	=3750.
;*
;*PULSE LENGTH CONSTANTS X 8 MSEC
;*
MTRTIM	=6			;METER PULSE
THPTIM	=3			;THUMPER
SLGTIM	=7			;SLINGSHOT
DRPTIM	=10.			;DROP TARGET
HKTIM	=6			;HOLE KICKER
OHLTIM	=15.			;OUTHOLE KICKER
SLDTIM  =30.			;SLINGSHOT DELAY TIME
;*
;*LAMP NUMBERS
;*
B1LMP	=0			;BONUS LAMPS
B2LMP	=1
B3LMP	=2
B4LMP	=3
B5LMP	=4
B6LMP	=5
B7LMP	=6
B8LMP	=7
B9LMP	=8
B10LMP	=9
B20LMP	=10.
B30LMP	=11.
DBLMP	=12.			;DOUBLE BONUS
TBLMP	=13.			;TRIPLE BONUS
SPSLMP	=14.			;SAME PLAYER SHOOTS AGAIN
BALLMP	=15.			;BALL
MCHLMP	=16.			;MATCH
TLTLMP	=17.			;TILT
P1MLMP	=18.			;MILLION LAMPS
P2MLMP	=19.
P3MLMP	=20.
P4MLMP	=21.
LN1LMP	=22.			;P
LN2LMP	=23.			;I
LN3LMP	=24.			;P
LN4LMP	=25.			;E
LN5LMP	=26.			;L
LN6LMP	=27.			;I
LN7LMP	=28.			;N
LN8LMP	=29.			;E
LSPLMP	=30.			;LFT SPINNER
RSPLMP	=31.			;RGT SPINNER
LTRGLMP	=32.			;L CAPTIVE TARGET
CTRGLMP	=33.			;C CAPTIVE TARGET
RTRGLMP	=34.			;R CAPTIVE TARGET
LD1LMP	=35.			;LFT DROP 3K
LD2LMP	=36.			; "   "   5K
CD1LMP	=37.			;CENT "   3K
CD2LMP	=38.			; "   "   5K
CD3LMP	=39.			; "   "  10K
RD1LMP	=40.			;RT   "   3K
RD2LMP	=41.			; "   "   5K
LTHLMP	=42.			;L THUMPER
RTHLMP	=43.			;R THUMPER
LSCLMP	=44.			;L SPECIAL
RSCLMP	=45.			;R SPECIAL
LXBLMP	=46.			;L XTRA BALL
RXBLMP	=47.			;R XTRA BALL
RO1LMP	=48.			;UPPER LFT ROLLOVER
RO2LMP	=49.			;UPPER RT ROLLOVER
RO3LMP	=50.			;LFT UPPER CENTER ROLLOVER
RO4LMP	=51.			;UPPER CENTER ROLLOVER
RO5LMP	=52.			;RT UPPER CENTER ROLLOVER
RO6LMP	=53.			;LFT LOWER CENTER ROLLOVER
RO7LMP	=54.			;RT LOWER CENTER ROLLOVER
GMOLMP	=55.			;GAME OVER LAMP
LOFF	=64.
;*DIP SWITCH ADDRS
SW1T1	=200B			;PROG SW1 TOGGLE 1
SW1T2	=200A
SW1T3	=2009
SW1T4	=2008
SW1T5	=200F
SW1T6	=200E
SW1T7	=200D
SW1T8	=200C
SW2T1	=2003			;PROG SW2 TOGGLE 1
SW2T2	=2002
SW2T3	=2001
SW2T4	=2000
SW2T5	=2007
SW2T6	=2006
SW2T7	=2005
SW2T8	=2004
;*
;*LAMP CONSTANTS - ADDRS+MASKS
;*
RO1ADD	=30			;UPPER LEFT ROLLOVER
RO1MSK	=1
RO2ADD	=30			;UPPER RT ROLLOVER
RO2MSK	=2
RO3ADD	=34			;LFT UPPER CENT ROLLOVER
RO3MSK	=4
RO4ADD	=38			;UPPER CENT ROLLOVER
RO4MSK	=4
RO5ADD	=3C			;RT UPPER CENT ROLLOVER
RO5MSK	=4
RO6ADD	=3D			;LFT LOWER CENT ROLLOVER
RO6MSK	=2
RO7ADD	=31			;RT LOWER CENT ROLLOVER
RO7MSK	=4
LTHADD	=39			;LFT THUMPER
LTHMSK	=1
RTHADD	=3D			;RT THUMPER
RTHMSK	=1
LSPADD	=35			;LFT SPINNER
LSPMSK	=4
RSPADD	=34			;RT SPINNER
RSPMSK	=1
LTADD	=31			;LFT CAPTIVE TARGET
LTMSK	=2
CTADD	=35			;CENT CAPTIVE TARGET
CTMSK	=2
RTADD	=31			;RT CAPTIVE TARGET
RTMSK	=1
LXBADD	=34			;LFT XTRA BALL
LXBMSK	=8
RXBADD	=3C			;RT XTRA BALL
RXBMSK	=1
LSCADD	=30			;LFT SPECIAL
LSCMSK	=8
RSCADD	=38			;RT SPECIAL
RSCMSK	=1

;*
	.SBTTL	ATTRACT MODE
;*
	.=7000
CKSUM1:  .BYTE 0A6		;CHECKSUM FIRST ROM
;*
;*ATTRACT MODE
;*
ATTRCT:	JSR	ATTST
ATTRLP:	JSR	CLRBAL
	BSR	INIT
	JSR	ATTR
	JSR	BATTR
	JSR 	P14
	JSR	MTAIN
	LDX	ATBLK		;CHECK DISPLAY BLANK
	BEQ	10$
	DEX
	STX	ATBLK
	BNE	10$
	JSR	DISBLK
10$:	BRA	ATTRLP
;*
;*CHECK PROGRAM STATUS
;*
STATCK:	LDAA	RTEST		;CHECK RAM TEST
	CMPA	I,0AA
	BNE	BADCK		;FAIL
	LDAA 	RTEST+1
	CMPA	I,55
	BNE 	BADCK		;FAIL
	TSX
	LDAA	X,0
	CMPA	I,6F		;PC IN RANGE?
	BLS	BADCK		;NO
	CMPA	I,7F
	BHI	BADCK	
	STX	TEMPI		;CHECK STACK POINTER
	LDAA	TEMPI	
	BNE	BADCK		;INVALID
	LDAA	TEMPI+1
STAT1:	CMPA	I,RTEST+1
	BLS	BADCK		;INVALID
	RTS
BADCK:	JMP	PWRON
;*
;*NMI ROUTINE
;*
NMI:	LDAA	RTEST		;CHECK RAM
	CMPA	I,0AA
	BNE	BADCK		;FAIL
	LDAA	RTEST+1
	CMPA	I,55
	BNE	BADCK		;FAIL
	TSX
	LDAA	X,5		;PC IN RANGE
	CMPA	I,6F
	BLS	BADCK		;FAIL
	CMPA	I,7F
	BHI	BADCK		;FAIL
	STX	NMITMP		;CHECK STACK PTR.
	LDAA	NMITMP
	BNE	BADCK
	LDAA	NMITMP+1
	BSR	STAT1
	RTI
;*
;*INITIALIZE OPTIONS
;*
INIT:	CLRA			;SETUP REPLAY SCORES
	LDX	I,204F
20$:	LDAB	X,0
	ROLB
	ROLA
	DEX
	CPX	I,204B
	BNE	20$
	ASLA
	LDX	I,REPTAB	;GET TABLE ADDR
	JSR	INDEX
	LDAA	X,0
	STAA	LOREP
	LDAA	E,SW1T3		;1ST RELAY ONLY?
	BMI	23$		;NO
	CLRA
	BRA	25$
23$:	LDAA	X,1
25$:	STAA	MREP
	BEQ	27$		;MR DEACTIVATED
	LDAB	E,SW1T8		;ADD-A-BALL
	BMI	27$		;NO
	ADDA	I,3		;ADD 30K TO MR
	DAA
	STAA	MREP
27$:	LDX	I,SW1T6		;GET MAX CREDIT MODE
	JSR	GMODE
	LDX	I,CRDTAB
	JSR	INDEX
	LDAA	X,0
	STAA	MAXCRD
	LDAA	I,3		;GET BALL MODE
	LDAB	E,SW2T1
	BPL	30$
	ADDA	I,2
30$:	STAA	MAXB
	RTS
;*
;*GET OPTION SWITCH MODE
;*INPUT=ADDR	;OUTPUT 0-1-2-3  SW. STATE ADDR+1, ADDR
GMODE:	CLRA
	LDAB	X,0
	ROLB
	ROLA
	LDAB	X,1
	ROLB
	ROLA
	RTS
;*
;*FLASH PLAYER 1-4 LITES
;*
P14:	DEC	P14TMP
	BPL	30$
	LDAA	I,50.
	STAA	P14TMP
	JSR	OFPLAN
	LDAA	PLAYN
	CMPA	I,4
	BNE	20$
	CLRA
20$:	INCA
	STAA	PLAYN
	JSR	ONPLAN
30$:	RTS
;*INIT ATTRACT MODE
ATTST:	LDX  I,ATRTAB
	STX ATNDEX
	LDX  I,BATTAB
	STX BNDEX
	CLRA 
	STAA ATIMER
	STAA BTIMER
	RTS
;*ATTRACT MODE
ATTR:	LDAA ATIMER	;FRAME OVER?
	BEQ ATT0	;YES
	DEC ATIMER	;NO, EXIT
	RTS
ATT0:	LDX ATNDEX
	LDAA X,0	;START OVER
	INCA
	BNE ATT01	;YES
	JSR ATTST	;RESTART
	BRA ATT0
ATT01:	INCA		;LOOP "N" TIMES?
	BNE ATT1
	INX
	LDAA X,0
	STAA LPCNT	;SET LOOP COUNTER
	BRA ATT10
ATT1:	INCA
	BNE ATT2
	DEC LPCNT
	BEQ ATT10
ATT1A:	DEX
	LDAA X,0
	CMPA  I,0FE	;LOOP BACK
	BNE ATT1A
	INX
	BRA ATT10
ATT2:	INCA		;CHANGE FRAME TIMER?
	BNE ATT3
	INX
	LDAA X,0
	STAA AFRAME
	BRA ATT10
ATT3:	INCA		;CUE?
	BNE ATT4
	LDX BNDEX	;CHANGE BGROUND
ATT3LP:	LDAA X,0
	BEQ ATT3A
	INX
	BRA ATT3LP
ATT3A:	INX
	STX BNDEX
	LDX ATNDEX
	BRA ATT10
ATT4:	LDAA	X,0
	JSR LAMP
	TSTA 
	BMI ATTX1
ATT10:	INX
	STX ATNDEX
	BRA ATT0
ATTX1:	LDAA AFRAME
	STAA ATIMER
	INX	;INCREMENT INDEX
	STX ATNDEX
	RTS
;*BACKGROUND PROCESS CALLED EVERY & MSEC.
BATTR:	LDAA BTIMER	;FRAME OVER?
	BEQ BATT0	;YES
	DEC BTIMER	;NO, EXIT
	RTS
BATT0:	LDX BNDEX
BATT1:	LDAA X,0	;END SEQUENCE?
	CMPA I,0FF
	BNE BATT2	;NO
BATT1A:	DEX	;LOOP BACK
	LDAA X,0
	INCA
	BNE BATT1A
	BRA BATT10
BATT2:	CMPA  I,0FC	;NEW FRAME VALUE
	BNE BATT3
	INX
	LDAA X,0
	STAA BFRAME
	BRA BATT10
BATT3:	JSR LAMP	;LIGHT LAMP
	TSTA 	;CHECK END OF FRAME
	BMI BATTX1
BATT10:	INX
	STX BNDEX
	BRA BATT0
BATTX1:	LDAA BFRAME	;RESET TIMER
	STAA BTIMER
	INX	;INCREMENT INDEX
	STX BNDEX
BATTX:	RTS

	.SBTTL	PLAYFIELD SWITCH & GAME PROCESSING
;*
;*BEGIN BALL ROUTINE
;*
BEGBAL:	JSR	LAMPOF		;TURN OFF LAMPS
	CLRA			;INITIALIZE PARAMETERS
	STAA	SCRFLG
	STAA	DTBFLG
	STAA	BCNT
	STAA	LDPFLG
	STAA	CDPFLG
	STAA	RDPFLG
	STAA	TRGFLG
	STAA	DRPFLG
	STAA	HKCNT
	STAA	ALTFLG
	COMA			;ON FLIPPERS
	STAA	FLIPEN
	LDAA	BALLN		;DISPLAY BALL #
	ORAA	I,0F0
	STAA	BALL
	LDX	I,18.		;CLEAR SWITCHES
15$:	CLR	X,PSWRAM-1
	DEX
	BNE	15$
	JSR	PMGET		;GET MEMORY
	JSR	DRPLIT		;LITE PLAYFIELD LAMPS
	JSR	TRGLIT
	JSR	PIPLIT
	LDAA	I,RO1LMP	;LITE STAR ROLLOVERS
20$:	JSR	LAMP
	INCA
	CMPA	I,RO7LMP+1
	BNE	20$
	LDAA	I,60.		;RESET DROP TARGETS
	STAA	DRPCNT
BEG1:	LDAA	I,OHLTIM	;EJECT BALL
	STAA	OUTHOL
;*
;*PLAYFIELD SWITCH PROCESSOR
;*ACCB=TABLE INDEX, X=SO ADDR
GAME:
PSWITCH:JSR	SWSCAN		;SCAN SWITCHES
	LDX	I,PSLACH
	LDAB	I,0FF
5$:	LDAA	X,0
	BNE	10$
	INX
	ADDB	I,8
	CMPB	I,47.		;DONE ALL SWITCHES?
	BNE	5$
	BRA	50$		;FINISH 
10$:	INCB			;FIND SWITCH
	LSRA
	BCC	10$
	PSHB			;SAVE B
	ANDB	I,7
20$:	ASLA			;RESET BIT
	DECB
	BPL	20$
	STAA	X,0		;STORE RESET MASK
	PULA			;FIND VECTOR
	LDX	I,SWTAB
	ASLA
	JSR	INDEX
	LDX	X,0
	BEQ	50$		;INVALID VECTOR
	JSR	X,0		;HANDLE SWITCH
50$:	JSR	MTAIN
	BRA	PSWITCH

	.SBTTL	PLAYFIELD SWITCH PROCESSING
;*PLAYFIELD SWITCH PROCESSING
;*
;* ROLLOVERS
;*
RO1SW:	LDAA	RO1ADD		;LIT?
	ANDA	I,RO1MSK
	BEQ	RO50		;NO, 50 PTS
	LDAA	I,RO1LMP+LOFF	;LOFF LAMP, 500 PTS
	BRA	ROCD
RO2SW:	LDAA	RO2ADD		;LIT?
	ANDA	I,RO2MSK
	BEQ	RO50		;NO, 50 PTS
	LDAA	I,RO2LMP+LOFF	;OFF LAMP, 500 PTS
	BRA	ROCD
RO4SW:	LDAA	RO4ADD		;LIT?
	ANDA	I,RO4MSK
	BEQ	RO50		;NO, 50 PTS
	LDAA	I,RO4LMP+LOFF
ROCD:	INC	CDPFLG		;ADVANCE CENT. DROP TARGET VALUE
	BRA	RO500		;AWARD 500 PTS
RO3SW:	LDAA	RO3ADD		;LIT?
	ANDA	I,RO3MSK
	BEQ	RO50		;NO, 50 PTS
	LDAA	I,RO3LMP+LOFF
	BRA	ROLD
RO6SW:	LDAA	RO6ADD		;LIT?
	ANDA	I,RO6MSK
	BEQ	RO50		;NO 50 PTS
	LDAA	I,RO6LMP+LOFF
ROLD:	INC	LDPFLG		;ADVANCE LEFT DROP VALUE
	BRA	RO500
RO5SW:	LDAA	RO5ADD		;LIT?
	ANDA	I,RO5MSK
	BEQ	RO50		;NO 50 PTS
	LDAA	I,RO5LMP+LOFF	;OFF
	BRA	RORD
RO7SW:	LDAA	RO7ADD		;LIT?
	ANDA	I,RO7MSK
	BEQ	RO50		;NO 50 PTS
	LDAA	I,RO7LMP+LOFF
RORD:	INC	RDPFLG		;ADVANCE RT DROP VALUE
RO500:	JSR	LAMP		;OFF ROLLOVER
	JSR	DRPLIT		;LITE DROP TARGETS
	LDAA	HUNSND		;500 PTS
	ADDA	I,5
	STAA	HUNSND
	RTS			;RETURN
RO50:	LDAA	TENSND		;50 PTS
	ADDA	I,5
	STAA	TENSND
	RTS			;RETURN
;*
;*PIPELINE LANES
;*
LN1SW:	LDAA	I,80		;P LANE
	BRA	LNPIP
LN2SW:	LDAA	I,40		;I LANE
	BRA	LNPIP
LN3SW:	LDAA	I,20		;P LANE
	BRA	LNPIP
LN4SW:	LDAA	I,10		;E LANE
	BRA	LNPIP
LN5SW:	LDAA	I,8		;L LANE
	BRA	LNPIP
LN6SW:	LDAA	I,4		;I LANE
	BRA	LNPIP
LN7SW:	LDAA	I,2		;N LANE
	BRA	LNPIP
LN8SW:	LDAA	I,1		;E LANE
LNPIP:	ORAA	PIPFLG
	CMPA	PIPFLG		;CHANGE IN STATUS?
	BEQ	50$		;NO
	STAA	PIPFLG		;UPDATE FLAG
	COMA			;TRIPLE BONUS
	BNE	10$		;NO
	LDAA	I,DBLMP+LOFF
	JSR	LAMP
	LDAA	I,TBLMP
	BRA	30$
10$:	LDAB	DTBFLG		;ALREADY EARNED
	BNE	40$		;YES
	BITA	I,0F		;DOUBLE BONUS?
	BEQ	20$		;YES
	BITA	I,0F0		;DOUBLE BONUS?
	BNE	40$		;NO
20$:	LDAA	I,DBLMP		;ON LAMP
30$:	JSR	LAMP
	LDAA	I,10.		;DOUBLE/TRIPLE BONUS SOUND
	STAA	DTBSND
	INC	DTBFLG		;DOUBLE/TRIPLE BONUS FLAG
40$:	JSR	PIPLIT		;LITE PIPELINE
50$:
FOURSW:	INC	K1SND		;SCORE 1000
	JMP	BONADV		;ADVANCE BONUS, RTS
;*
;*THUMPER BUMPERS
;*
LTHSW:  LDAA	I,THPTIM
	STAA	LTHUMP
	LDAA	LTHADD
	ANDA	I,LTHMSK	;LIT?
	BEQ	TH100		;NO
	BRA	TH1000
RTHSW:	LDAA	I,THPTIM	;HIT SOLENOID
	STAA	RTHUMP
	LDAA	RTHADD		;LIT?
	ANDA	I,RTHMSK
	BEQ	TH100		;NO
TH1000:	INC	K1SND		;1K PTS
	RTS
TH100:	INC	HUNSND		;100 PTS
	RTS
;*
;*SLINGSHOTS
;*
RSLSW:	LDAA	RSLDCT		;INHIBITIONS?
	BNE	SLINGX		;YES
	LDAA	I,SLDTIM		;INHIBIT
	STAA	RSLDCT
	LDAA	I,SLGTIM	;FIRE SOLENOID
	STAA	RSLING
	BRA	SLING1
LSLSW:	LDAA	LSLDCT		;INHIBITIONS?
	BNE	SLINGX		;YES
	LDAA	I,SLDTIM		;INHIBIT
	STAA	LSLDCT
	LDAA	I,SLGTIM	;FIRE SOLENOID
	STAA	LSLING
SLING1:	JSR	ALT		;ALTERNATE TH,SPEC,XB
	INC	TENSND
	INC	TENSND
SLINGX:	RTS
;*
;*TEN POINT SWITCH
;*
TENSW:	INC	TENSND		;TEN PTS.
	RTS
;*
;*FIFTY POINT SWITCH
;*
FIFTYSW:LDAA	I,5		;50 PTS
	ADDA	TENSND
	STAA	TENSND
;*
	RTS
;*CAPTIVE BALL TARGETS
;*
LTRGSW:	LDAA	I,1
	BRA	TARGET
CTRGSW:	LDAA	I,2
	BRA	TARGET
RTRGSW:	LDAA	I,4
	BRA	TARGET
TARGET:	ORAA	TRGFLG		;CHANGE?
	CMPA	TRGFLG
	BEQ	50$		;NO
	STAA	TRGFLG
	CMPA	I,7		;START SPECIAL?
	BNE	10$		;NO
	LDAA	E,SW1T4		;LIB?
	BMI	15$		;YES
	LDAA	DRPFLG		;DROPS DOWN?
	CMPA	I,7
	BNE	10$		;NO
15$:	LDAA	I,4		;YES, START SPECIAL
	JSR	ALTST
10$:	JSR	TRGLIT		;LITE TARGETS
50$:	LDAA	I,3		;3000 PTS
	ADDA	K1SND
	STAA	K1SND
	JMP	BONADV		;ADVANCE BONUS, RTS
;*
;*DROP TARGETS
;*
LDRPSW:	LDAB	DRPCNT		;INHIBIT SCORE ON RESET
	BNE	DRPX
	LDAA	I,LSPLMP
	JSR	LAMP
	LDAA	LDPFLG
	LDAB	I,1
	BRA	DROP
CDRPSW:	LDAB	DRPCNT		;INHIBIT SCORE ON RESET
	BNE	DRPX
	LDAA	I,1		;ON THUMPER ALTERNATION
	JSR	ALTST
	LDAA	CDPFLG
	LDAB	I,2
	BRA	DROP
RDRPSW:	LDAB	DRPCNT		;INHIBIT SCORE ON RESET
	BNE	DRPX
	LDAA	I,RSPLMP	;ON RT SPINNER
	JSR	LAMP
	LDAA	RDPFLG
	LDAB	I,4
DROP:	INC	K1SND		;AWARD APPROPRIATE POINTS
	INC	K1SND		;0=2K, 1=3K, 2=5K, 3=10K
	DECA
	BMI	50$
	INC	K1SND
	DECA
	BMI	50$
	INC	K1SND
	INC	K1SND
	DECA
	BMI	50$
	LDAA	I,5
	ADDA	K1SND
	STAA	K1SND
50$:	ORAB	DRPFLG		;ALL DOWN?
	STAB	DRPFLG
	CMPB	I,7
	BNE	DRPX		;NO
	LDAA	TRGFLG		;SPECIAL?
	CMPA	I,7
	BNE	55$		;NO
	LDAA	I,4
	BRA	60$
55$:	LDAA	XFLG		;XBALL ALREADY?
	BNE	DRPX		;YES
	LDAA	I,2		;START XBALL ALTERNATION
60$:	JSR	ALTST
DRPX:	JMP	DRPLIT		;LITE DROP TARGETS, RTS
;*
;*SPINNERS
;*
RSPNSW:	LDAA	RSPADD		;LIT?
	ANDA	I,RSPMSK
	BNE	SPN1K		;YES
	BRA	SPN100		;NO
LSPNSW:	LDAA	LSPADD		;LIT?
	ANDA	I,LSPMSK
	BNE	SPN1K		;YES
SPN100:	CLR	HUNCNT		;INTERRUPT SOUND, 100 PTS
	INC	HUNSND
	RTS
SPN1K:	CLR	K1CNT		;INTERRUPT SOUND, 1000 PTS
	INC	K1SND
	RTS
;*
;*HOLE KICKERS
;*
OHOLSW:
LHKSW:
RHKSW:	LDAA	I,40.		;START DEBOUNCE
	STAA	HKCNT
	RTS
;*
;*END OF BALL
;*
ENDBAL:	LDS	I,0FF		;LOAD STACK
	JSR	MTAIN
	LDX	I,TONST		;WAIT FOR ALL TO FINISH
	CLRA
10$:	ORAA	X,0
	INX
	CPX	I,TONEND
	BNE	10$
	TSTA
	BNE	ENDBAL
	LDAA	SCRFLG		;SCORED
	BNE	20$		;YES
	JMP	BEG1		;NO, RESTART BALL
20$:	JSR	BONCOL		;COLLECT BONUS
END1:	JSR	PMSAVE		;SAVE MEMORY
	JSR	OFPLAN		;OFF PLAYER-UP LIGHT
	LDAA	XFLG		;XTRA BALL?
	BEQ	30$		;NO
	DEC	PLAYN		;YES
30$:	LDAA	PLAYN		;INC PLAYER NUMBER
	CMPA	MAXP
	BNE	40$
	LDAB	BALLN
	CMPB	MAXB
	BEQ	50$
	INCB
	STAB	BALLN
	CLRA
40$:	INCA
	STAA	PLAYN
	JMP	BEGBAL
50$:	LDAA	I,GMOLMP	;LITE GAME OVER
	JSR	LAMP
	CLRA
	STAA	GMFLG		;CLEAR GAME, COIN FLAGS
	STAA	LCFLG
	STAA	RCFLG
	STAA	FLIPEN		;OFF FIPPERS
	LDAA	I,BALLMP+LOFF
	JSR	LAMP
	LDAA	E,SW2T2		;MATCH?
	BPL	MCHX		;NO
	LDAA	I,MCHLMP
	JSR 	LAMP		;ON MATCH
;*
;*MATCH ROUTINE
;*
MATCH:	LDX	I,MCHTAB	;GET TABLE INDEX
	LDAA	SEED		;STORE MATCH NUMBER
	STAA	BALL
	CLRA
	STAA	MCHTMP
5$:	LDAA	X,0
	BEQ	MCHEK
	INX
10$:	LDAB	MCHTMP		;KEEP SOUND STATIONARY
	STAB	MCHCNT		;FORCE SOUND
	JSR	MTAIN
	DECA
	BNE	10$
	INC	MCHTMP		;UPDATE NUMBER
	LDAA	I,30
	ADDA 	BALL
	DAA
	STAA	BALL
	BRA	5$
MCHEK:	LDX 	I,0
5$:	LDAA	BALL
	CMPA	X,0
	BNE	10$
	JSR	ADCRED		;AWARD CREDIT
	LDAB	I,20.		;WAIT FOR METER
	JSR 	WAITLP
10$:	INX
	INX
	INX
	INX
	CPX	I,10
	BNE	5$
MCHX:	JMP	ATTRCT
;*
;*LITE CAPTIVE BALL TARGETS
;*
TRGLIT:	LDAB	TRGFLG
	LDAA	I,LTRGLMP
5$:	LSRB
	BCC	8$
	ADDA	I,LOFF		;OFF IF HIT
	JSR	LAMP
	SUBA	I,LOFF
	BRA	10$
8$:	JSR	LAMP		;ON IF NOT HIT
10$:	INCA
	CMPA	I,LTRGLMP+3
	BNE	5$
	RTS
;*
;*LITE DROP TARGETS
;*
DRPLIT:	LDAA	DRPFLG		;LEFT TARGET DOWN?
	CLRB
	BITA	I,1
	BNE	2$		;YES
	LDAB	LDPFLG
2$:	LDAA	I,LD1LMP
	DECB
	BPL	4$
	ADDA	I,LOFF
4$:	JSR	LAMP
	LDAA	I,LD2LMP
	DECB
	BPL	6$
	ADDA	I,LOFF
6$:	JSR	LAMP
	LDAA	DRPFLG
	CLRB
	BITA	I,2		;CENTER TARGET DOWN
	BNE	12$		;YES
	LDAB	CDPFLG
12$:	LDAA	I,CD1LMP
	DECB
	BPL	14$
	ADDA	I,LOFF
14$:	JSR	LAMP
	LDAA	I,CD2LMP
	DECB
	BPL	16$
	ADDA	I,LOFF
16$:	JSR	LAMP
	LDAA	I,CD3LMP
	DECB
	BPL	18$
	ADDA	I,LOFF
18$:	JSR	LAMP
	LDAA	DRPFLG
	CLRB
	BITA	I,4		;RIGHT TARGET DOWN
	BNE	22$		;YES
	LDAB	RDPFLG
22$:	LDAA	I,RD1LMP
	DECB
	BPL	24$
	ADDA	I,LOFF
24$:	JSR	LAMP
	LDAA	I,RD2LMP
	DECB
	BPL	26$
	ADDA	I,LOFF
26$:	JMP	LAMP		;ON LAMP, RTS
;*
;*RESET STATE ROUTINE
;*
RESET:	LDAA	I,5		;SCORE 5000
	ADDA	K1SND
	STAA	K1SND
	LDAB	I,55.		;WAIT FOR SOUND
	JSR	WAITLP
	LDAA	I,60.		;RESET TARGETS
	STAA	DRPCNT
	LDAA	I,LSPLMP+LOFF	;OFF SPINNER LAMPS
	JSR	LAMP
	INCA
	JSR	LAMP
	CLRA
	STAA	DRPFLG		;RESET DROP TARGETS
	LDAB	E,SW1T4		;RESET SPECIAL?
	BMI	10$		;NO
	STAA	ALTFLG		;CLEAR SPECIAL
10$:	LDAA	ALTFLG		;SAVE SPECIAL
	ANDA	I,4
	STAA	ALTFLG
	JSR	ALT1
	JSR	DRPLIT
	JSR	TRGLIT
RESX:	RTS
;*BONUS ADVANCE ROUTINE
BONADV:	LDAA	BCNT		;MAXIMUM BONUS?
	CMPA	I,MAXBON
	BEQ	RESX		;YES
	INC	BCNT
	LDAA	I,3		;BONUS ADVANCE SOUND
	ADDA	BONSND
	STAA	BONSND
;*LITE BONUS
;*TURN BONUS OFF FIRST
BLITE:	LDAA	I,B1LMP+LOFF
5$:	JSR	LAMP
	INCA
	CMPA	I,B30LMP+LOFF+1
	BNE	5$
;*LITE BONUS
	LDAB	BCNT
	CMPB	I,30.		;30K?
	BNE	10$		;NO
	LDAA	I,B30LMP
	BRA	50$
10$:	CMPB	I,20.		;20K?
	BLT	20$		;NO
	LDAA	I,B20LMP
	SUBB	I,20.
	BRA	30$
20$:	CMPB	I,10.		;10K?
	BLT	40$
	SUBB	I,10.
	LDAA	I,B10LMP
30$:	JSR	LAMP
40$:	TSTB			;LITE ANY MORE?
	BEQ	60$		;NO
	LDAA	I,B1LMP-1
	ABA
50$:	JSR	LAMP		;LITE 1-9
60$:	RTS
;*
;*BONUS COLLECT ROUTINE
;*
BONCOL:	LDAA	BCNT
	PSHA			;SAVE BCNT
5$:	BSR	BLITE		;LITE BONUS
10$:	JSR	MTAIN
	LDAA	K1SND		;WAIT FOR SOUND
	ORAA	K1CNT
	BNE	10$
	DEC	BCNT
	BMI	20$
	INC	K1SND
	BRA	5$
20$:	PULA
	STAA	BCNT
	LDAA	DTBFLG
	BEQ	50$		;EXIT, DONE
	DECA
	BEQ	30$
	LDAA	I,TBLMP+LOFF	;OFF TRIPLE
	JSR	LAMP
	LDAA	I,DBLMP		;ON DOUBLE
	BRA	35$
30$:	LDAA	I,DBLMP+LOFF	;OFF DOUBLE
35$:	JSR	LAMP
	DEC	DTBFLG
	BSR	BLITE		;LITE BONUS
	LDAB	I,25.
	JSR	WAITLP		;PAUSE
	BRA	BONCOL
50$:	RTS
;*
;*START ALTERNATION
;*
ALTST:	ORAA	ALTFLG		;ADD TO FLAG, GO TO ALT
	STAA	ALTFLG
	BRA	ALT1
;*
;*ALTERNATION ROUTINE
;*
ALT:	LDAA	ALTCNT		;ALTERNATE
	INCA
	ANDA	I,1
	STAA	ALTCNT
ALT1:	LDAB	ALTFLG
	BITB	I,4		;SPECIAL OVERRIDE XBALL
	BEQ	5$
	ANDB	I,0FD		;OVERRIDE
	STAB	ALTFLG
5$:	LDAA	I,LTHLMP+LOFF	;OFF ALL
10$:	JSR	LAMP
	INCA
	CMPA	I,RXBLMP+LOFF+1
	BNE	10$
	LSRB			;THUMPER ALT?
	BCC	20$		;NO
	LDAA	I,LTHLMP	;YES, LIGHT
	ADDA	ALTCNT
	BSR	LAMP
20$:	LSRB			;XBALL ALT?
	BCC	30$		;NO
	LDAA	I,LXBLMP	;YES, LIGHT
	ADDA	ALTCNT
	BSR	LAMP
30$:	LSRB			;SPECIAL ALT?
	BCC	50$		;NO
	LDAA	I,LSCLMP	;YES LIGHT
	ADDA	ALTCNT
	BSR	LAMP
50$:	RTS			;EXIT
;*SAVE PLAYER MEMORY
PMSAVE:	LDAA	PLAYN		;CALL ADDR
	LDX	I,PMEM-1
	BSR	INDEX
	LDAA	PIPFLG
	COMA
	BITA	I,0F		;RESET 'LINE' IF ACHIEVED
	BNE	10$
	ORAA	I,0F
10$:	BITA	I,0F0		;RESET 'PIPE' IF ACHIEVED
	BNE	20$
	ORAA	I,0F0
20$:	COMA
	STAA	X,0
	RTS
;*GET PLAYER MEMORY
PMGET:	LDAA	PLAYN
	LDX	I,PMEM-1
	BSR	INDEX
	LDAA	X,0
	STAA	PIPFLG		;GET MEMORY
	JMP	PIPLIT		;LITE IT UP
;*LITE PIPELINE
PIPLIT:	LDAB	PIPFLG
	LDAA	I,LN8LMP
5$:	LSRB
	BCS	10$
	BSR	LAMP		;LITE THOSE NOT ACHIEVED
	BRA	40$
10$:	ADDA	I,LOFF		;OFF LAMP
	BSR 	LAMP
	SUBA	I,LOFF
40$:	DECA
	CMPA	I,LN1LMP-1
	BNE	5$
	RTS
;
;*INDEX ROUTINE X=ACCA+X
;
INDEX:	STX	TEMPI
	ADDA	TEMPI+1
	STAA	TEMPI+1
	BCC	20$
	INC	TEMPI
20$:	LDX	TEMPI
	RTS
;*LAMP ACTIVATION ROUTINE
;*ACCA=LAMP #0-63=ON
LAMP:	STX	TEMP3
	PSHA
	PSHB
	CLRB
	ANDA	I,7F		;MASK BIT 7
	CMPA	I,63.
	BLS	10$
	SUBA	I,64.
	COMB
10$:	LDX	I,LMPTAB	;GET TABLE ADDR
	BSR	INDEX
	LDAA	X,0		;FETCH OFFSET
	LSRA
	LSRA
	LSRA
	LSRA
	STAA	TEMPI+1
	CLR	TEMPI
	LDAA	X,0		;GET MASK
	LDX	TEMPI		;FORMULATE INDEX
	TSTB			;ON OR OFF?
	BPL	20$		;ON
	COMA
	ANDA	X,30		;OFF LAMP
	BRA	30$
20$:	ORAA	X,30
30$:	STAA	X,30
	LDX	TEMP3
	PULB
	PULA
	RTS
;*ON/OF PLAYER-UP LIGHT SUBROUTINE
OFPLAN:	CLRB 
	BRA ONPN1
ONPLAN:	LDAB  I,8
ONPN1:	JSR  CADDR
	STAB X,3
	RTS
;*LOAD DISPLAY + MATCH CREDIT
DBLK1:	LDAA	I,0FF
DLD1:	LDX	I,1D
	BRA	DISPLP
;*DISPLAY LOAD SUBROUTINE, CHAR TO LOAD IN ACCA
DISBLK:	LDAA	I,0FF
DISPLD:	LDX  I,<^H0F>
DISPLP:	STAA X,0
	DEX
	BNE DISPLP
	STAA X,0
	RTS
;*LAMPON/OFF TURNS ALL LAMPS ON/OFF
LAMPON:	LDAA  I,<^H0FF>
LON2:	LDX  I,<^H30>	;LAMP START ADDR
LON1:	STAA  X,0
	INX
	CPX  I,<^H40>
	BNE LON1
	RTS
LAMPOF:	CLRA 
	BRA LON2

	.SBTTL	OPERATING SYSTEM SWITCH PROCESSING
;*
;*OPERATING SYSTEM SWITCH PROCESSING
;*
OPSWITCH: LDAA	TESTSW		;CHECK SWITCH
	ANDA	I,1F
	CMPA	I,0F
	BNE	30$		;NO GOOD
	LDAA	TSTFLG		;GET TEST NUMBER
	INCA
	STAA	TSTFLG
	CMPA	I,4		;THRU?
	BLT	20$
	LDS	I,0FF		;WAIT FOR LETUP
10$:	JSR	MTAIN
	LDAA	TESTSW
	BNE	10$
	JMP	PWRON		;YES
20$:	TAB			;DISPLAY TEST #
	ORAB	I,0F0
	STAB	0
	DECA
	ASLA
	LDX	I,TSTTAB
	JSR	INDEX
	LDX	X,0
	LDS	I,0FF		;REST STACK
	JMP	X,0		;JUMP TO NEXT TEST
30$:	LDAA	TSTFLG		;IF TEST EXIT
	ORAA	LCKFLG		;IFTILT EXIT
	BNE	100$
	JSR	COIN		;CHECK COINS
	LDAA	STRTSW		;START?
	ANDA	I,0F		;24 MSEC. MIN
	CMPA	I,7
	BNE	40$		;NO
	JSR	START
40$:	LDAA	SLMSW
	BSR	BITCNT
	CMPB	I,1
	BHI	TILT
50$:	LDAA	TLTCSW		;16 MSEC MIN
	BSR	BITCNT
	CMPB	I,1
	BHI	TILT
60$:	LDAA	TLTPSW
	BSR	BITCNT
	CMPB	I,1
	BHI	TILT
70$:
100$:	RTS
;*
;*COUNT 1'S BITS IN ACCA RESULT IN ACCB
;*
BITCNT:	LDAB	I,8		;BIT COUNT
	STAB	TEMPI
	CLRB
5$:	LSRA
	BCC	10$
	INCB			;COUNT 1'S
10$:	DEC	TEMPI
	BNE	5$
	RTS
;*
;*TILT ROUTINE
;*
TILT:	LDS	I,0FF		;RESET STACK
	BSR	TONCLR		;CLEAR SOUNDS, SCORES
	LDAA	I,10.		;TILT SOUND
	STAA	TLTSND
	CLRA			;OFF FLIPPERS
	STAA	FLIPEN
	STAA	HKCNT		;OFF HOLE
	STAA	XFLG
	INCA
	STAA	LCKFLG		;LOCKOUT COIS
	JSR	LAMPOF		;ALL LAMPS OFF
	LDAA	I,TLTLMP	;LITE TILT LAMP
	JSR	LAMP
	LDAA	GMFLG		;GAME ON?
	BEQ	40$		;NO
	STAA	SCRFLG		;SCORED FLAG ON
	JSR	ONPLAN		;ON PLAYER UP LIGHT
10$:	JSR	MTAIN		;WAIT FOR BALL IN OUTHOLE
	BSR	CLRBAL
	LDAA	E,OHOLE		;CHECK OUTHOLE
	BPL	10$
20$:	CLRB			;WAIT FOR TILT TO FINISH
30$:	JSR	MTAIN
	LDAA	SLMSW
	ORAA	TLTCSW
	ORAA	TLTPSW
	BNE	20$
	DECB
	BNE	30$
	CLR	LCKFLG		;LET COINS IN
	JMP	END1		;END BALL
40$:	LDAB	I,50.		;ATTRACT MODE TILT
	JSR	W100
	CLR	LCKFLG
	JMP	ATTRCT
;*CLEAR TONE & SCORE COUNTERS
TONCLR:	LDX	I,TONST
TONCLP:	CLR	X,0
	INX
	CPX	I,TONEND
	BNE	TONCLP
	RTS
;*
;*CLEAR BALL FROM PLAYFIELD
;*
CLRBAL:	LDAA	E,LHOLE		;LEFT HOLE?
	BPL	10$		;NO
	LDAA	I,HKTIM
	STAA	LHKICK
10$:	LDAA	E,RHOLE
	BPL	20$
	LDAA	I,HKTIM		;RT HOLE?
	STAA	RHKICK		;NO
20$:	RTS
;*START ROUTINE
START:	LDAA	CRED		;ZERO CREDIT?
	BEQ	10$		;YES, EXIT
	LDAA	GMFLG		;1ST START?
	BEQ	20$		;YES
	LDAA	MAXP		;4 PLAYERS?
	CMPA	I,4
	BEQ	10$		;YES
	LDAA	BALLN
	CMPA	I,1		;PAST 1ST BALL?
	BEQ	40$		;NO
10$:	RTS			;EXIT
20$:	CLRA
	STAA	MAXP
	STAA	ATBLK		;STOP DISPLAY BLANK
	STAA 	ATBLK+1
	INCA			;1ST START
	STAA	BALLN
	STAA	PLAYN
	STAA	GMFLG
	JSR	DISBLK		;BLANK DISPLAY
	LDX	I,4		;ZERO OUT SCORE AREA+MEMORY
30$:	CLR	X,PMEM-1
	DEX
	BNE	30$
	LDX	I,10
35$:	CLR	X,DISP1-1
	DEX
	BNE	35$
40$:	LDAA	I,3		;PUT ZERO IN DISPLAY
	SUBA	MAXP
	ASLA
	ASLA
	LDX	I,0
	JSR	INDEX
	CLR	X,0
	INC	MAXP		;INCR # OF PLAYERS
	INC	STSND		;MAKE SOUND
	LDAA	I,MTRTIM	;PULSE PLAY METER
	STAA	PLAMTR
	LDAA	I,99		;DEC CREDIT
	JSR	CLIT0
	LDAA	MAXP
	CMPA	I,1		;START BALL?
	BNE	10$
	LDS	I,0FF		;YES
	JMP	BEGBAL
;*
;*GET COIN MODE
;*ACCB=COIN CHUTE  0=LEFT, 1=RIGHT
CMODE:	LDX	I,SW2T6		;GET MODE
	JSR	GMODE
	PSHA
	LDX	I,SW2T4
	JSR	GMODE
	PULB
	ASLB
	ASLB
	ABA
	ASLA			;CALL ADDR MULT X4
	ASLA
	LDX	I,CMTAB		;GET INDEX
	JSR	INDEX
	LDAA	X,0
	STAA	LCMODE		;LEFT COIN MODE
	LDAA	X,1
	STAA	LCMODE+1
	LDAA	X,2
	STAA	RCMODE		;RT COIN MODE
	LDAA	X,3
	STAA	RCMODE+1
	RTS
;*
;*COIN ROUTINE
;*
COIN:	BSR	CMODE
	LDAA	LCNSW		;LEFT COIN SWITCH?
	ANDA	I,0F
	CMPA	I,7
	BNE	10$		;NO
	LDAB	LCFLG		;GET FLAG
	LDX	I,LCMODE
	BSR	KOIN		;AWARD CREDITS, METER COUNTS
	STAB	LCFLG		;UPDATE FLAG
10$:	LDAA	RCNSW		;RT COIN SWITCH?
	ANDA	I,0F
	CMPA	I,7
	BNE	20$		;NO
	LDAB	RCFLG		;GET FLAG
	LDX	I,RCMODE
	BSR	KOIN		;AWARD CREDITS, METER COUNTS
	STAB	RCFLG		;UPDATE FLAG
20$:	RTS
;*
;*COIN HANDLER
;*ACCB=COIN FLAG
KOIN:	TSTB			;1ST COIN?
	BNE	10$		;NO
	LDAA	X,0
	LSRA			;GET CREDITS
	LSRA
	LSRA
	LSRA
	INCB			;INC FLAG
	BRA	50$
10$:	CMPB	I,1		;2ND COIN?
	BNE	20$		;NO
	LDAA	X,1		;2 COIN MODE?
	BITA	I,0F0
	BEQ	12$		;YES
	ADDB	I,2		;NO, INC FLAG
12$:	DECB
	LDAA	X,0
	ANDA	I,0F
	BRA	50$
20$:	CLRB			;3RD COIN
	LDAA	X,1
	LSRA
	LSRA
	LSRA
	LSRA
50$:	PSHB
	JSR	CLITE		;AWARD CREDITS
	PULB
	LDAA	X,1
	ANDA	I,0F
	ADDA	CNCNT
	STAA	CNCNT
	LDAA	RCMODE		;MOVES SAME?
	CMPA	LCMODE
	BNE	60$		;NO
	LDAA	RCMODE+1
	CMPA	LCMODE+1
	BNE	60$		;NO
	STAB	LCFLG		;YES, MAKE FLAGS SAME
	STAB	RCFLG
60$:	RTS

	.SBTTL	 MAINTAINANCE ROUTINE & SUBROUTINES
;*
;*MAINTAINANCE ROUTINE
;*
MTAIN:	PSHA
	PSHB
	STX	TEMP5
	JSR 	SOD		;SOLENOID MAINTAINANCE
	JSR 	SCAUD		;SCORE MAINT
	LDAA	TSTFLG		;SKIP OVER FOR TEST MODE
	BNE	30$
	LDAA	LSLDCT		;SLINGSHOT INHIBIT DELAYS
	BEQ	10$
	DEC	LSLDCT
10$:	LDAA	RSLDCT
	BEQ	20$
	DEC	RSLDCT
20$:	JSR	LCKOUT		;LOCKOUT COIL MAINT
	LDAA	GMFLG		;NOT GAME SKIP	
	BEQ	30$
	BSR	DRPRES		;DROP RESET MAINT
	JSR	HOLEK		;EJECT HOLE MAINT.
	JSR	LFLASH		;LAMP FLASHER
	JSR	XFLASH		;FLASH SPSA
	JSR	FLASH		;FLASH PLAYER UP
30$:	JSR 	OPSCAN		;OPERATING SYSTEM SWTICH SCAN
	JSR 	OPSWITCH	;OP.SYSTEM SWITCH PROCESSING
	JSR	CMETER		;HANDLE COIN METER
	JSR	STATCK		;CHECK PROGRAM STATUS
	BSR	WAIT
	LDX	TEMP5
	PULB
	PULA
	RTS
;*WAIT FOR PHASE CHANGE ROUTINE
WAIT:	LDAA	E,<^H2000>
	ANDA  I,<^H40>
	CMPA FRTMP
	BEQ WAIT	;CONTINUE WAITING
	STAA	E,WRES	;STOP WAKEUP RESET
	STAA FRTMP
;*TIME COUNTER
	LDAA	GMFLG		;SCOPE
	BPL	20$		;NO	
	LDX	HZ
	DEX
	BNE	10$
	LDAA	I,MTRTIM	;PULSE MIN. COUNTER
	STAA	TIMTR
	LDX	I,MINTIM
10$:	STX	HZ
20$:	LDAA	I,10		;INC MATCH RANDOM #
	ADDA	SEED
	DAA
	STAA	SEED
	RTS
;*WAIT DELAY SUBROUTINE DELAY=ACCB X 8 MSEC.
WAITLP:	JSR MTAIN
	DECB 
	BNE WAITLP
	RTS
;*WAIT 100 MSEC
W100:	PSHB
	LDAB	I,12.		;12X8 MSEC.
	BSR	WAITLP
	PULB
	DECB
	BNE	W100
	RTS
;*
;*DROP TARGET RESET ROUTINE
;*
DRPRES:	LDAA	DRPCNT		;COUNT ON?
	BEQ	50$		;NO
	LDAB	I,DRPTIM	;LOAD PULSE TIMER
	CMPA	I,50.		;LEFT DROP TARGET?
	BNE 	10$		;NO
	STAB	LDROP
	BRA	40$
10$:	CMPA	I,40.
	BNE	20$
	STAB	CDROP
	BRA	40$
20$:	CMPA	I,30.
	BNE	40$
	STAB	RDROP
40$:	DEC	DRPCNT
50$:	RTS
;*
;*HOLE KICKER CHECK
;*
HOLEK:	LDAA	HKCNT		;DEBOUNCE ACTIVE?
	BEQ	50$		;NO
	DEC	HKCNT
	BNE	50$		;TIME TO CHECK?
	LDAA	E,OHOLE		;OUTHOLE
	BPL	5$
	JMP	ENDBAL
5$:	LDAA	E,LHOLE		;BALL IN LEFT HOLE?
	BPL	10$		;NO
	LDAB	LXBADD		;CHECK XBALL
	ANDB	I,LXBMSK
	LDAA	I,LXBLMP
	BSR	XCHEK
	LDAB	LSCADD
	ANDB	I,LSCMSK
	LDAA	I,LSCLMP
	BSR	SCHEK		;CHECK SPECIAL
	JSR	RESET
	LDAA	I,HKTIM
	STAA	LHKICK
	RTS
10$:	LDAA	E,RHOLE
	BPL	50$
	LDAB	RXBADD		;CHECK XBALL
	ANDB	I,RXBMSK
	LDAA	I,RXBLMP
	BSR	XCHEK
	LDAB	RSCADD		;CHECK SPECIAL
	ANDB	I,RSCMSK
	LDAA	I,RSCLMP
	BSR	SCHEK
	JSR	RESET
	LDAA	I,HKTIM
	STAA	RHKICK
50$:	RTS
;*
;*CHECK XTRA BALL
;*
XCHEK:	TSTB			;AWARD XBALL
	BEQ	50$
	STAA	LAMPFL		;FLASH LAMP
	LDAB	I,140.
	STAB	LFCNT
	LDAA	E,SW1T2		;XBALL, YES
	BMI	10$		;NO
	LDAA	I,10.
	STAA	X25SND		;25K PTS
	BRA	20$
10$:	JSR	XBALL		;WAIT FOR REWARD
20$:	JSR	WAITLP
50$:	RTS
;*
;*CHECK SPECIAL
;*
SCHEK:	TSTB			;XTRA BALL?
	BEQ	50$		;NO
	PSHA
10$:	LDAA	I,MTRTIM	;HIT SPECIAL METER
	STAA	SPMTR
	CLRA
	STAA	TRGFLG		;CLEAR CAPT
	STAA	ALTFLG		;CLEAR SPECIAL
	LDX	I,SW2T8		;GET SPECIAL MODE
	JSR	GMODE
	DECA
	BPL	20$
	LDAA	I,20.		;LOOK
	BRA	35$
20$:	DECA
	BPL	30$
	JSR	ADBAL1		;XBALL
	BRA	45$
30$:	BNE	40$
	LDAA	I,10.
35$:	STAA	SP5SND
	BRA	47$
40$:	JSR	ADCRED		;REPLAY
45$:	LDAA	I,16.
	STAA	SPSND
47$:	PULA
	STAA	LAMPFL
	LDAA	I,0FF
	STAA	LFCNT
48$:	JSR	MTAIN
	LDAA	SPCNT
	ORAA	SPSND
	ORAA	SP5CNT
	ORAA	SP5SND
	BNE	48$
	CLR	LFCNT
50$:	RTS
;*
;*FLASH SPSA
;*
XFLASH:	LDAA	I,SPSLMP
	LDAB	XFLG		;XTRA BALL?
	BEQ	25$		;NO
	DEC	XFCNT
	BMI	20$
	LDAB	XFCNT
	CMPB	I,15
	BHI	25$
	BRA	30$		;ON SPSA
20$:	LDAB	I,20
	STAB	XFCNT
25$:	ADDA	I,LOFF
30$:	JMP	LAMP		;ON/OFF SPSA, RTS
;*
;*LAMP FLASH ROUTINE
;*
LFLASH:	LDAB	LFCNT
	BEQ	5$
	LDAA	LAMPFL
	DECB
	STAB	LFCNT
	ANDB	I,0F
	BEQ	10$
	CMPB	I,8
	BEQ	20$
5$:	RTS
10$:	ADDA	I,LOFF
20$:	JMP	LAMP		;JSR LAMP, RTS
;*
;*FLASH PLAYER-UP
;*
FLASH:	LDAA 	SCRFLG		;SCORED?
	BNE	10$		;YES
	DEC	FLCNT
	BMI	20$
	LDAA	FLCNT
	CMPA	I,25		;ON OR OFF
	BHI	30$		;OFF
10$:	JMP	ONPLAN		;ON PLAYER-UP LIGHT
20$:	LDAA 	I,40
	STAA	FLCNT
30$:	JMP 	OFPLAN		;OFF PLAYER-UP LIGHT
;*OP SYSTEM SWITCHES
OPSCAN:	LDAA	E,200B		;TEST SWITCH
	ROLA
	ROL	SWRAM
	LDAA	E,2010
	ROLA
	ROL	SWRAM+1
	LDAA	E,2011
	ROLA
	ROL	SWRAM+2
	LDAA	E,2012
	ROLA
	ROL	SWRAM+3
	LDAA	E,2013
	ROLA
	ROL	SWRAM+4
	LDAA	E,2020
	ROLA
	ROL	SWRAM+5
	LDAA	E,2021
	ROLA
	ROL	SWRAM+6
	RTS
;*
;*PLAYFIELD SWITCH SCAN
;*
SWSCAN:	LDX	I,2020
	STX	TEMP1
	LDX	I,PSWRAM
	STX	TEMP2
10$:	LDX	TEMP1
	LDAB	X,0
	ROLB
	RORA			;GET 8 SWITCHES
	LDAB	X,1
	ROLB
	RORA
	LDAB	X,2
	ROLB
	RORA
	LDAB	X,3
	ROLB
	RORA
	LDAB	X,4
	ROLB
	RORA
	LDAB	X,5
	ROLB
	RORA
	LDAB	X,6
	ROLB
	RORA
	LDAB	X,7
	ROLB
	RORA
	LDX	TEMP2
	TAB			;SAVE CURRENT WORD
	COMB			;0=ON, 1=OFF
	ANDA	X,0		;AND DEBOUNCERS
	ANDA	X,6
	ORAA	X,12.
	STAA	X,12.		;SET TRIGGER
	LDAA	X,0		;SHIFT UP DEBOUNCERS
	STAA	X,6
	STAB	X,0
	INC	TEMP2+1
	LDAA	TEMP1+1
	ADDA	I,8
	STAA	TEMP1+1
	CMPA	I,50
	BNE	10$
	RTS
;*
;*COIN METER PULSE ROUTINE
;*
CMETER:	LDAA	CDELAY		;DELAY ON?
	BEQ	10$		;NO
	DEC	CDELAY		;YES
	RTS
10$:	LDAA	CNCNT		;COINS TO COUNT?
	BEQ	50$		;NO
	DEC	CNCNT
	LDAA	I,MTRTIM		;PULSE METER
	STAA	CNMTR
	ASLA
	STAA	CDELAY		;DELAY COUNT
50$:	RTS
;*
;*LOCKOUT COIL MAINT
;*
LCKOUT: CLRB
	LDAA	LCKFLG		;TILT?
	BNE	20$		;YES
	LDAA	CRED
	CMPA	MAXCRD		;MAX CREDIT?
	BGE	20$		;NO
	COMB			;ON COIL
20$:	STAB 	LKCOIL
	RTS
;*
;*SOLENOID MAINTAINANCE
;*
SOD:	LDX	I,SOLRAM	;RAM COUNTERS
	STX	TEMP1
	LDX	I,SOLTAB	;ROM TABLES
	STX	TEMP2
	LDAA	I,10		;BASE ADDR
	STAA	TEMP3
10$:	LDX	TEMP1
	LDAA	X,0		;ON OR OFF
	BEQ	20$		;OFF
	BMI	15$		;ALWAYS ON?
	DEC	X,0		;ON
15$:	LDX	TEMP2
	LDAA	X,0		;GET OFFSET
	STAA	TEMP3+1
	LDAB	X,1		;GET MASK
	LDX	TEMP3
	ORAB	X,0		;TURN ON
	BRA	30$
20$:	LDX	TEMP2		;SOLENOID OFF
	LDAA	X,0		;GET OFFSET
	STAA	TEMP3+1
	LDAB	X,1		;GET MASK
	LDX	TEMP3
	COMB
	ANDB	X,0		;TURN OFF
30$:	STAB	X,0
	INC	TEMP1+1		;INC RAM COUNTER
	LDX	TEMP2
	INX
	INX
	STX	TEMP2
	CPX	I,SOLTEND	;END OF TABLE
	BNE	10$		;NO
	RTS
;*
;*SOUND & SCORE ROUTINE
;*
SCAUD:	LDX	I,TONST		;GET STARTING ADDR
	STX	TEMP1
	CLR	TEMP2
SCLP:	LDX	TEMP1
	LDAA	X,0		;TONE CNT>0
	BNE	SC1
	LDAA	X,1		;SND>0
	BEQ	SCINC		;NO
SCO:	DEC	X,1		;DEC SOUND
	CLRA			;START TONE CNT
SC1:	INCA			;INC COUNT
	STAA	X,0
	LDAA	TEMP2
	LDX	I,SNDTAB
	JSR	INDEX
	STX	TEMP3
	LDAA	WAVE
	ANDA	I,0F0
	LDAB	X,2
	ABA
	STAA	E,WAVE+1000	;WAVE
	LDAA	X,3		;GET 1/2 WAY
	LDAB	X,4		;GET LENGTH
	LDX	TEMP1
	CMPA	X,0		;1/2 WAY
	BHI	SC2
	TST	X,1		;MORE SOUNDS
	BNE	SCO		;YES
SC2:	CMPB	X,0		;FINISHED
	BGE	SC3		;NO
	CLR	X,0		;CLEAR SOUND,FINISHED
SCINC:	LDAA	TEMP2		;INCREASE OFFSET
	ADDA	I,7
	STAA	TEMP2
	INX
	INX
	STX	TEMP1
	CPX	I,TONEND	;DONE
	BNE	SCLP		;NO
	STAA	E,AUDOFF	;DISABLE AUDIO
SCEX:	RTS
SC3:	STAA	E,AUDOFF	;ENABLE AUDIO
	STAA	E,AUDON
	LDAA	X,0
	PSHA
	LDX	TEMP3		;GET ADDR
	LDX	X,0
	DECA
	JSR	INDEX
	LDAA	AMP		;AMP
	ANDA	I,0F0		;SAVE SOLENOIDS
	LDAB	X,0
	ANDB	I,0F
	ABA
	STAA	E,AMP+1000	;STORE AMP.
	LDAA	FREQ		;FREQUENCY
	ANDA	I,0F0		;SAVE SOLENOIDS
	LDAB	X,0
	LSRB
	LSRB
	LSRB
	LSRB
	ABA
	STAA	E,FREQ+1000	;STORE FREQ
SC4:	PULA			;SCORE 1ST TIME?
	CMPA	I,1
	BNE	SCEX		;NO, EXIT
	LDX	TEMP3
	LDAA	X,5		;1000,100'S
	CMPA	I,0FF		;NO SCORE?
	BEQ	SCEX		;YES, EXIT
	LDAB	X,6		;10'S, 1'S
	BRA	SCORE		;SCORE
;*CALC PLAYER SCORE ADDR
CADDR:	LDAA	I,4
	SUBA	PLAYN
	ASLA
	ASLA
	CLR	TEMPI
	STAA	TEMPI+1
	LDX	TEMPI
	RTS
;*SCORE BLANKING ROUTINE
SBLANK:	BSR  CADDR	;GET CURRENT PLAYER SCORE ADDR
	LDAA  I,3
SBLOOP:	LDAB  X,<^H22>
	BMI SLOAD
	TST TEMPI
	BNE SLOAD1
	CMPB  I,<^H0F>	;TOP 4 BITS ZERO?
	BGT SLOAD	;NO
	CMPB  I,0	;ZERO BYTE?
	BNE SB1	;NO
	COMB 
	BRA SLOAD1
SB1:	ORAB  I,<^H0F0>	;BLANK HALF-BYTE
SLOAD:	INC TEMPI
SLOAD1:	STAB  X,2
	DEX
	DECA 
	BNE SBLOOP
	RTS
;*SCORE ROUTINE 1000'S, 100'S ACCA	;10'S ACCB
SCORE:	PSHA			;SAVE SCORE INCS
	PSHB
	LDAA	SCRFLG		;SCORED?
	BNE	10$		;YES
	STAA	XFLG		;CLR XBALL FLAG
	COMA
	STAA	GMFLG		;SET SCORED
	INC	SCRFLG		;NO, INDICATE SCORE
10$:	BSR	CADDR		;CALC. PLAYER SCORE ADDR
	PULA
	ADDA	X,DISP1		;10'S
	DAA
	STAA	X,DISP1
	PULA
	ADCA	X,DISP1+1	;1000'S, 100'S
	DAA
	STAA	X,DISP1+1	;SAVE CURRENT LSB
	LDAA	X,DISP1+2	;100K, 10K
	STAA	TEMP1		;SAVE PREVIOUS MSB
	ADCA	I,0
	DAA
	STAA	X,DISP1+2
	STAA	TEMP2		;SAVE CURRENT MSB
	BSR	SBLANK
	LDAA	LOREP
	BSR	REPLCK
	LDAA	MREP
;*REPLAY CHECKER
;*ACCA=REPLAY LEVEL
;*TEMP1=PREV. SCORE
;*TEMP2=CURR. SCORE
REPLCK:	CMPA	TEMP1		;ALREADY AWARDED?
	BLS	REPX		;YES
	CMPA	TEMP2
	BLS	REPLAY
REPX:	RTS
;*AWARD REPLAY
REPLAY:	LDAA	I,MTRTIM	;HIT METER
	STAA	REPMTR
	LDAA	E,SW1T8		;CREDIT OR ADD-A-BALL
	BPL	ADBAL
	LDAA	I,1
	BRA	CLITE		;AWARD REPLAY
;*ADD-A-BALL
XBALL:	LDAA	I,MTRTIM	;HIT METER
	STAA	XBMTR
ADBAL:	LDAA	I,10.		;MAKE SOUND
	STAA	XBSND
ADBAL1:	INC	XFLG		;ON FLAG AND SOUND
	RTS
;*ADD CREDIT FOR REPLAY
;
ADCRED:	LDAA	I,MTRTIM	;ON REPLAY METER
	STAA	REPMTR
	LDAA	I,1
;
;*CREDIT HANDLER
;
CLITE:	TAB			;MAKE CREDIT SOUNDS
	ADDB	CRDSND
	STAB	CRDSND
CLIT0:  ADDA	I,0		;CONVERT TO BCD
	DAA
	ADDA	CRED
	DAA
	CMPA	MAXCRD		;CHECK MAXIMUM CREDIT
	BLS	CLIT1
	LDAA	MAXCRD
CLIT1:	STAA	CRED
	STAA	CREDIT		;DISPLAY CREDIT
	RTS

	.SBTTL	SELF-TEST AND POWER ON
;*
;*POWER ON
;*
PWRON:	LDS	I,0FF		;SETUP STACK
	CLR	E,1080		;OFF SOLENOIDS
	CLR	E,1084
	CLR	E,1088
	CLR	E,108C
	LDAA	I,55		;RAM TEST BYTE
	STAA	RTEST+1
	COMA
	STAA	RTEST
	LDX	I,RTEST-1
10$:	CLR	X,0
	DEX
	BNE	10$
	LDAA	I,0FF
	LDX	I,SLMSW-SWRAM	;MAKE OP SYS SWITCH BAD
20$:	STAA	X,SWRAM-1
	DEX
	BNE 	20$
	LDAA	E,SW1T1
	BPL	15$
	INC	TSTFLG
	BRA	TEST
15$:	LDAA	I,88		;88'S IN DISPLAY
	JSR	DLD1
	LDX	I,MINTIM	;SETUP TIMER
	STX	HZ
	STX	ATBLK
	JMP	ATTRCT
;*
;*TEST MODE
;*
;*LAMP,DATA DISPLAY, ROM TEST
TEST:	JSR	TONCLR		;CLEAR ALL SCORING
	JSR	LAMPON		;LAMPS ON
	CLRB
	STAB	GMFLG
	COMB
	CLRA
	LDX	I,7000		;CHECK ROM1
11$:	ADDA	X,0
	STAA	E,WRES		;STOP WAKEUP
	INX
	CPX	I,7800
	BNE	11$
	TSTA
	BEQ	12$
	ANDB	I,1F		;BAD
12$:	CLRA			;CHECK ROM2
13$:	ADDA	X,0
	STAA	E,WRES		;STOP WAKEUP
	INX
	CPX	I,8000
	BNE	13$
	TSTA
	BEQ	15$
	ANDB	I,0F2		;BAD
15$:	STAB	TSTTMP
27$:	JSR	DISBLK
	CLRB
	LDAA	MREP		;DISPLAY MED REPLAY?
	BEQ	30$		;NO
	STAA	0A
	STAB	9
	STAB	8
30$:	LDAA	LOREP		;DISPLAY LO REPLAY?
	BEQ	40$		;NO
	STAA	0E
	STAB	0D
	STAB	0C
40$:	LDAA	MAXB		;DISPLAY MAX. BALLS
	STAA	BALL
	LDAA	MAXCRD		;DISPLAY MAX. CREDIT
	STAA	CREDIT
	LDAA	I,0F1		;DISPLAY TEST#
	STAA	0
	LDAA	TSTTMP		;STORE ROM CK
	STAA	2
	JSR	INIT		;GET SETTINGS
	JSR 	MTAIN
	BRA	27$
;*
;*SOLENOID TEST
;*
TEST2:	COM	BALL
	JSR	LAMPOF
	LDX	I,SOLRAM
	LDAA	I,1
10$:	STAA	CREDIT
	LDAB	I,10.		;ACTIVATE SOLENOID 1ST TIME
	STAB	X,0
	LDAB	I,100.
	JSR 	WAITLP		;WAIT
	LDAB	I,10.		;ACTIVATE SOLENOID 2ND TIME
	STAB	X,0
	LDAB	I,100.
	JSR	WAITLP		;WAIT
	LDAB	STRTSW		;MOVE TO NEXT ON START
	BEQ	10$		;NO
	ADDA	I,1
	DAA
	INX
	CPX	I,SOLMTR
	BNE	10$
	BRA	TEST2
;*
;*SWITCH TEST
;*
TEST3:	CLRA
	STAA	CREDIT
	LDX	I,2010
	JSR	MTAIN
10$:	ADDA	I,1
	DAA
	LDAB	X,0
	BPL	20$
	STAA	CREDIT
	LDAB	I,1
	STAB	TENCNT
	LDAB	I,80
	JSR	WAITLP
20$:	INX
	CPX	I,204C
	BNE 	10$
	BRA	TEST3

	.SBTTL	TABLES
;*
;*SOUND TABLES
;*
SNDTAB:
;*TILT SOUND
	.WORD	TLTPAT		;SOUND PATTERN
	.BYTE	8		;WAVE
	.BYTE	25.		;1/2 LENGTH
	.BYTE	25.		;TOTAL LENGTH
	.WORD	0FF00		;SCORE, FF=0
;*CREDIT SOUND
	.WORD	CRDPAT
	.BYTE	2
	.BYTE	18.
	.BYTE	27.
	.WORD	0FF00
;*START SOUND
	.WORD K1PAT
	.BYTE 2
	.BYTE 14.
	.BYTE 14.
	.WORD 0FF00
;*SPECIAL SOUND
	.WORD	SPPAT
	.BYTE	9
	.BYTE	15.
	.BYTE	15.
	.WORD	0FF00
;*SPECIAL 5K SOUND
	.WORD	SPPAT
	.BYTE	9
	.BYTE	15.
	.BYTE	15.
	.WORD	5000
;*XTRA BALL SOUND
	.WORD	EXBPAT
	.BYTE	0B
	.BYTE	14.
	.BYTE	14.
	.WORD	0FF00
;*XTRA BALL 25K SOUND
	.WORD	EXBPAT
	.BYTE	0B
	.BYTE	14.
	.BYTE 	14.
	.WORD	2500
;*DOUBLE TRIPLE BONUS SOUND
	.WORD	DTBPAT
	.BYTE	0A
	.BYTE	16.
	.BYTE	16.
	.WORD	0FF00
;*BONUS ADVANCE SOUND
	.WORD	BAPAT
	.BYTE	4
	.BYTE	12.
	.BYTE	12.
	.WORD	0FF00
;*1K SOUND
	.WORD	K1PAT
	.BYTE	0
	.BYTE	12.
	.BYTE	15.
	.WORD	1000
;*100PT SOUND
	.WORD	HUNPAT
	.BYTE	3
	.BYTE	12.
	.BYTE	24.
	.WORD	100
;*10PT SOUND
	.WORD	TENPAT
	.BYTE	2
	.BYTE	8
	.BYTE	16.
	.WORD	10
;*MATCH
	.WORD DTBPAT
	.BYTE 3
	.BYTE 18.
	.BYTE 18.
	.WORD 0FF00
;*
;*SOUND PATTERNS
;*
HUNPAT:	.BYTE	1F,2E,3D,4C,5B,6A,79,88
	.BYTE	97,0A6,0B5,0C4,0D3,0E2,0F1,11
	.BYTE	22,33,44,55,66,77,88,99
TLTPAT:	.BYTE	1D,1D,2D,3D,4D,5D,6D,7D
	.BYTE	8E,9E,0AE,0BE,0CE,0DE,0EF,0DE
	.BYTE	0CD,0DE,0CD,0DE,0CD,0DE,0CD,0DE
	.BYTE	0CD
CRDPAT:	.BYTE	88,88,9,0AA,0AA,0AA,0AA,0BB
	.BYTE	0CC,0CC,0CC,0CC,0CC,0DD,0EE,0EE
	.BYTE	0EE,0EE,0EE,0EE,0FF,0FF,0FF,0FF
	.BYTE	0FF,0FF,0EE
K1PAT:
SPPAT:
EXBPAT:	.BYTE	1F,2F,3F,4F,5F,6F,7F,8F
	.BYTE	9F,0AF,0BF,0CF,0DF,0EF
DTBPAT:	.BYTE	1C,0FF,2C,0EE,3C,0DD,4C,0CC
	.BYTE	5C,0BB,6C,0AA,7C,9C,8C,8C
TENPAT: .BYTE	0DD,0CC,0BB,0AA,99,88,77,67
	.BYTE	57,47,37,33,33,22,22,22
BAPAT:	.BYTE	0DF,0CF,0BF,0AF,9F,8F,7F,6F
	.BYTE	5F,0F,3F,0F
;*LAMP TABLE
LMPTAB:	.BYTE	0B1,0F1,32,72,0B2,0F2,34,74	;0-7
	.BYTE	0B4,0F4,38,78,31,71,28,68	;8-15
	.BYTE	61,21,24,64,0A4,0E4,42,82	;16-23
	.BYTE	0C2,04,22,62,0A2,0E2,54,41	;24-31
	.BYTE	12,52,11,94,0D4,18,58,92	;32-39
	.BYTE	51,0A1,91,0D1,08,81,48,0C1	;40-47
	.BYTE	01,02,44,84,0C4,0D2,14,0E1	;48-55
;*SOLENOID TABLE
SOLTAB:	.BYTE	84,20		;LEFT THUMPER
	.BYTE	80,80		;RT THUMPER
	.BYTE	8C,10		;LEFT SLINGSHOT
	.BYTE	8C,20		;RT SLINGSHOT
	.BYTE	84,80		;OUTHOLE KICKER
	.BYTE	8C,40		;LEFT DROP TARGET
	.BYTE	88,80		;CENTER DROP TARGET
	.BYTE	8C,80		;RIGHT DROP TARGET
	.BYTE	84,10		;LEFT HOLE KICKER
	.BYTE	80,40		;RT HOLE KICKER
	.BYTE	80,20		;LOCKOUT COIL
	.BYTE	88,40		;FLIPPER CONTROL
	.BYTE	84,40		;XTRA BALL METER
	.BYTE	8C,01		;REPLAY METER
	.BYTE	8C,02		;TOTAL PLAYS
	.BYTE	8C,04		;SPECIALS
	.BYTE	8C,08		;TIME
	.BYTE	80,10		;COIN COUNTER
SOLTEND:
;*SWITCH VECTOR TABLE
SWTAB:	.WORD	0		;SPARE
	.WORD	0
	.WORD	RO1SW		;UPPER LEFT ROLLOVER
	.WORD	RO2SW		;UPPER RT ROLLOVER
	.WORD	RO3SW		;LFT UPPER CENT ROLLOVER
	.WORD	RO4SW		;UPPER CENT ROLLOVER
	.WORD	LTHSW		;LFT THUMPER
	.WORD	RTHSW		;RT THUMPER
	.WORD	0		;SPARES
	.WORD	0
	.WORD	0
	.WORD	0
	.WORD	0
	.WORD	0
	.WORD	0	
	.WORD	0
	.WORD	RO5SW		;RT UPPER CENT ROLLOVER
	.WORD	RO6SW		;LFT LOWER CENT ROLLOVER
	.WORD	RO7SW		;RT LOWER CENT ROLLOVER
	.WORD	FOURSW		;RT, LFT DRAIN + SPINNER LANES
	.WORD	LN1SW		;P
	.WORD	LN2SW		;I
	.WORD	LN3SW		;P
	.WORD	LN4SW		;E
	.WORD	OHOLSW		;OUTHOLE
	.WORD	RSLSW		;RT SLINGSHOT
	.WORD	LSLSW		;LFT SLINGSHOT
	.WORD	RHKSW		;RT HOLE KICKER
	.WORD	LHKSW		;LFT HOLE KICKER
	.WORD	TENSW		;TEN PT
	.WORD	FIFTYSW		;FIFTY PT
	.WORD	0		;SPARE
	.WORD	LTRGSW		;LFT TARGET
	.WORD	CTRGSW		;C TARGET
	.WORD	RSPNSW		;RT SPINNER
	.WORD	RTRGSW		;R TARGET
	.WORD	LDRPSW		;L DROP TARGET
	.WORD	CDRPSW		;C DROP TARGET
	.WORD	LSPNSW		;LFT SPINNER
	.WORD	RDRPSW		;RT DROP
	.WORD	LN5SW		;L
	.WORD	LN6SW		;I
	.WORD	LN7SW		;N
	.WORD	LN8SW		;E
	.WORD	0
	.WORD	0
	.WORD	0
	.WORD	0
;*TEST MODE VECTOR TABLE
TSTTAB:	.WORD	TEST,TEST2,TEST3
;*
;*MAXIMUM CREDIT TABLE
;*
CRDTAB:	.BYTE	5,10,15,20
;*
;*REPLAY TABLE
;*
REPTAB:	.BYTE	0,0		;LEVEL 0
	.BYTE	12,16		;      1
	.BYTE	14,18		;      2
	.BYTE	16,20		;      3
	.BYTE	18,22		;      4
	.BYTE	20,24		;      5
	.BYTE	22,26		;      6
	.BYTE	23,28		;      7
	.BYTE	25,30		;      8
	.BYTE	27,32		;      9
	.BYTE	29,34		;     10
	.BYTE	31,36		;     11
	.BYTE	33,38		;     12
	.BYTE	35,40		;     13
	.BYTE	37,42		;     14
	.BYTE	39,44		;     15
;*
;*MATCH TABLE
;*
MCHTAB:	.BYTE	20.,18.,16.,16.,15.,15.,12.,10.
	.BYTE	10.,10.,8.,8,8,8,0
;*
;*ATTRACT MODE TABLE
;*
ATRTAB:	.BYTE	0FE,3		;LOOP 3 TIMES
	.BYTE	0FC,10.		;SET FRAME - TURN'EM ON
	.BYTE	9A,9D,9B,9C	;LEIN
	.BYTE	80,81,82,83,84	;12345
	.BYTE	85,86,87,88,89	;6789 10
	.BYTE	8A,8B,8C,8D	;20 30 DB TB
	.BYTE	9E,9F		;LR SPINNERS
	.BYTE	0A3,0A9		;L3K R5K
	.BYTE	0A4,0A8		;L5K R3K
	.BYTE	0A5,0A6,0A7	;C3K 5K 10K
	.BYTE	0B5,0B6		;R06 R07
	.BYTE	0A1		;C CAPT
	.BYTE	0AA,0AB		;LR THUMP
	.BYTE	0A0,0A2		;LR CAPT
	.BYTE	0AC,0AD,0AE,0AF	;LR SPEC, LR XB
	.BYTE	0B2,0B4,0B3	;R0E R03 R04
	.BYTE	96,99,98,97	;PEIP
	.BYTE	0B0,0B1		;R01 R02
;*TURN'EM OUT
	.BYTE	0FC,8		;SET FRAME 120 MS
	.BYTE	0C0,0C1,0C2,0C3,0C4,0C5,0C6,0C7	;BONUS
	.BYTE	0C8,0C9,0CA,0CB
	.BYTE	4C,0CD		;DB,TB
	.BYTE	5A,5B,5C,0DD	;LINE
	.BYTE	5E,0DF		;SPINNERS
	.BYTE	63,64,68,0E9	;L,R DROP
	.BYTE	65,66,0E7	;CENT DROP
	.BYTE	75,0F6		;R06,R07
	.BYTE	60,61,0E2	;CAPT BALL
	.BYTE	6A,0EB		;THUMPERS
	.BYTE	6E,0EF		;XBALL
	.BYTE	6C,0ED		;SPECIAL
	.BYTE	72,73,0F4	;R0345
	.BYTE	56,57,58,0D9	;PIPE
	.BYTE	70,0F1		;R01,R02
	.BYTE	0FD		;LOOP
	.BYTE	0FB		;CUE BGRND
	.BYTE	0FE,10.		;LOOP N TIMES
	.BYTE	0FC,10.		;SET FRAME
	.BYTE	9F		;ON LANES, DROPS, SPINNERS
	.BYTE	16,1A,0A9
	.BYTE	0A8
	.BYTE	17,1B,0A6
	.BYTE	0A7
	.BYTE	18,1C,0A5
	.BYTE	0A4
	.BYTE	19,1D,0A3
	.BYTE	9E
	.BYTE	0DF		;OFF LANES, DROPS, SPINNERS
	.BYTE	56,5A,0E9
	.BYTE	0E8
	.BYTE	57,5B,0E6
	.BYTE	0E7
	.BYTE	58,5C,0E5
	.BYTE	0E4
	.BYTE	59,5D,0E3
	.BYTE	0DE
	.BYTE	0FD		;LOOP
	.BYTE	0FF		;RESTART
;*
;*BACKGROUND MODE
;*
BATTAB:	.BYTE	0FC,15.		;FRAME=1/8 SEC
	.BYTE	0CE
	.BYTE	0FF		;END 1ST SET
	.BYTE	0C			;DB, TB
	.BYTE	0,44,6,4A,0B,47,5,41	;BONUS		1
	.BYTE	2E,2F			;SPEC, XBALL	1
	.BYTE	2A,6B			;THUMPERS	1
	.BYTE	30,74,36,72		;ROLLOVERS	1
	.BYTE	20,0E2			;CAPTIVE BALLS	1
	.BYTE	0D			;DB, TB
	.BYTE	4,42,0A,48,7,49,1,43	;BONUS		3
	.BYTE	6C,6D			;SPEC, XBALL	3
	.BYTE	2B,73			;THUMPERS	3
	.BYTE	34,71,32,75		;ROLLOVERS	3
	.BYTE	22,0E1			;CAPTIVE BALLS	3
	.BYTE	4C,4D			;DB, TB
	.BYTE	2,40,8,46,9,4B,3,45	;BONUS		2
	.BYTE	2C,2D,6E,6F		;SPEC, XBALL	2
	.BYTE	33,6A			;THUMPERS	2
	.BYTE	31,70,35,76		;ROLLOVERS	2
	.BYTE	21,0E0			;CAPTIVE BALLS	2
	.BYTE	0FF			;END
;*
;*COIN MODE TABLES
;*
;*FORMAT	1ST BYTE - B(4-7)=1ST COIN CREDIT
;*		1ST BYTE - B(0-3)=2ND COIN CREDIT
;*		2ND BYTE - B(4-7)=3RD COIN CREDIT
;*		2ND BYTE - B(0-3)=COIN METER COUNTS
CMTAB:  .BYTE	12,01,12,01	;2/3
	.BYTE	23,01,23,01	;2/5
	.BYTE	01,01,01,01	;2/1
	.BYTE	11,11,11,11	;1/1
	.BYTE	22,21,22,21	;1/2
	.BYTE	33,31,33,31	;1/3
	.BYTE	44,41,44,41	;1/4
	.BYTE	55,51,55,51	;1/5
	.BYTE	55,52,23,01	;L=1/5(2) R=2/5
	.BYTE	0EE,0E5,23,01	;L=1/14(5) R=2/5
	.BYTE	0CC,0C5,23,01	;L=1/12(5) R=2/5
	.BYTE	55,5A,22,25	;L=1/5(10) R=1/2
	.BYTE	66,65,02,01	;L=1/6(5) R=2/2
	.BYTE	0CC,0C5,22,21	;L=1/12(5) R=1/2
	.BYTE	0CC,0C5,04,01	;L=1/12(5) R=2/4
	.BYTE	01,11,01,11	;L=3/2 R=3/2
;*
;*GARBAGE
;*
GARB:	.BYTE	8,0,0F8,7F,72,1
	.BYTE	0,10,0,3,0,0F8,7F,7B
	.BYTE	0EB,7B,0EB,70,49,7B,0EB,8A
	.BYTE 	1,0,6,0,6,0,0F3,0
	.BYTE	0,0,0,0,0,0,0,0
	.BYTE	0,0,0,0,0,0,0
;*
;*VECTORS
;*
	.=7FF7
CKSUM2:	.BYTE	53		;2ND ROM CHECKSUM
	.WORD	PWRON,PWRON,NMI,PWRON
	.END
                                                                                                                                                                                                           